set term x11

eV = 1.60218e-19

nTerminalOutputs = 1

set ytics nomirror
set y2tics nomirror
set logscale y2
set format y2 "%g"

set xlabel "Iteration"
set ylabel "Number of DSMC particles  [million]"
set y2label "Average modal energy [eV]"

plot \
"< cat log.dsmcFoam+ | grep 'Number of DSMC particles' | cut -d '=' -f 2" u (($0+1)*nTerminalOutputs):($1/1e6) w l axis x1y1 t "Number of DSMC particles", \
"< cat log.dsmcFoam+ | grep 'Average linear kinetic energy' | cut -d '=' -f 2" u (($0+1)*nTerminalOutputs):($1/eV) w l axis x1y2 t "Average linear kinetic energy",\
"< cat log.dsmcFoam+ | grep 'Average rotational energy' | cut -d '=' -f 2" u (($0+1)*nTerminalOutputs):($1/eV) w l axis x1y2 t "Average rotational energy",\
"< cat log.dsmcFoam+ | grep 'Average vibrational energy' | cut -d '=' -f 2" u (($0+1)*nTerminalOutputs):($1/eV) w l axis x1y2 t "Average vibrational energy"

pause 10
reread 
